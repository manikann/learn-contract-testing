import org.springframework.cloud.contract.verifier.config.TestFramework
import org.springframework.cloud.contract.verifier.config.TestMode

plugins {
    id 'idea'
    id 'java'
    id 'maven'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "org.springframework.cloud.contract" version "2.2.3.RELEASE"
}

group = 'io.nataman'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ext {
    set('springCloudVersion', "Hoxton.SR5")
    set('rest-assured.version', '4.3.0')
    set('groovy.version', '3.0.4')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    // JSR 380 validator
    implementation 'org.hibernate.validator:hibernate-validator'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'


    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // testing
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation('org.junit.jupiter:junit-jupiter') {
        exclude group: 'junit', module: 'junit'
    }

    testImplementation 'io.projectreactor:reactor-test'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('org.springframework.cloud:spring-cloud-starter-contract-verifier') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // rest-assured for api testing
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "io.rest-assured:spring-web-test-client"

    // embedded mongo
    testRuntime 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

contracts {
    testFramework = TestFramework.JUNIT5
    testMode = TestMode.EXPLICIT
    baseClassForTests = "io.nataman.contract.Base"
}
